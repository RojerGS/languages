[Tic Tac Toe by Daniel Cristofani @ https://www.reddit.com/r/brainfuck/comments/joygbb/excellent_tictactoe_player/]
--->--->>>>->->->>>>>-->>>>>>>>>>>>>>>>>>+>>++++++++++[
  <<++[
    --<+<<+<<+>>>>[
      >[<->>+++>>[-]<<<+[<++>>+>>-<<<--]]+>+++++[>>++++++++++<<-]
      >>-.[-]>>+[<<<<+>>+>>-]<<<<<<[>+<-]<<
    ]++++++++++brainfuck.org[-]>++
  ]-->>[-->[-]>]<<[
    >>--[
      -[
        -[
          -[
            -[
              -[
                --[>+>+++++++<<+]>>-.----->,[<->-]<<[[->]<<]<<[>,[-]]>-->>[-]
              ]<[
                +[[<<]-[>>]<+<-]>[-<+]<<[<<]-<[>[+>>]>[>]+>[-]]
                <-[[+>>]<-->>[>]++++>>+>]
              ]>
            ]<[
              >-[+<+++]+<+++[+[---->]<<<<<<[>]>[<<[-]>>[>]>]]
              >[<+[---->]+[<]<[[>>]+>++++++++>+>]>>[[>]>>>]<<]<<+>++>
            ]>
          ]<[
            -[[>+>+<<-]>[<+>-]+++>>+++++>>]
            <[<<++[-->>[-]++>]>[[-]+>[<<+>>-]>]<]
          ]>
        ]<[
          -[
            --[+<<<<--[+>[-]>[<<+>+>-]<<[>>+<<-]]++[>]]
            <<[>>+>+<<<-]>>>[<<<+>>>-]>
          ]<[<<<[-]+++>[-]>[<+>>>+<<-]+>>>]+>
        ]>
      ]<[
        +[[<]<<[<<]-<->>+>[>>]>[>]<-]<[<]<<+<++[[>+<-]++<[<<->>+]<++]<
        <<<<<<      +> > >+> > >+[
        <<<               ->+>+>+[
        <<<<<<<   +>->+> > >->->+[
        <<<<<         ->+>+> >+>+[
        <<<<            ->->+>->+[
        <<<<<<<<+>-> >+> > >->+>+[
        <<<<<         -> >+> >->+[
        <<<<            +>->+> >+]]]]]]]
        +++[[>+<-]<+++]--->>[[<->-]<+++>>]++[[<->-]>>]>[>]
      ]<
    ]
  ]<
]